INCLUDE(${CMAKE_SOURCE_DIR}/cmake/mariadb_connector_c.cmake)
 
SET(CONNECTOR_C_INSTALLDIR "${CMAKE_CURRENT_BINARY_DIR}/mariadb-connector-c/${CMAKE_CFG_INTDIR}" CACHE STRING "")

IF(IS_DIRECTORY ${CMAKE_SOURCE_DIR}/mariadb-connector-c)
 # Building from source package
 SET(CONNECTOR_C_SOURCE_DIR  ${CMAKE_SOURCE_DIR}/mariadb-connector-c)
 SET(GIT_PARAMS)
ELSE()
 # Building from git, use git to get Connector/C
 SET(GIT_PARAMS 
  GIT_REPOSITORY "https://github.com/MariaDB/mariadb-connector-c/"
  GIT_TAG "master"
 )
 SET(CONNECTOR_C_SOURCE_DIR ${CMAKE_CURRENT_BINARY_DIR}/mariadb-connector-c-src)
ENDIF()

INCLUDE(ExternalProject)
ExternalProject_Add(
  mariadb_connector_c
  ${GIT_PARAMS}
  UPDATE_COMMAND ""
  PATCH_COMMAND ""
  SOURCE_DIR ${CONNECTOR_C_SOURCE_DIR}
  INSTALL_DIR ${CONNECTOR_C_INSTALLDIR}
  CMAKE_ARGS ${CC_CMAKE_OPTS}
  "-DCMAKE_INSTALL_PREFIX=${CONNECTOR_C_INSTALLDIR}"
  -DSKIP_TESTS=1
  -DCURL_FOUND=1
  TEST_COMMAND ""
)

ADD_LIBRARY(mariadbclient STATIC IMPORTED GLOBAL)
SET_TARGET_PROPERTIES(mariadbclient PROPERTIES IMPORTED_LOCATION
  "${CONNECTOR_C_INSTALLDIR}/lib/mariadb/${CMAKE_STATIC_LIBRARY_PREFIX}mariadbclient${CMAKE_STATIC_LIBRARY_SUFFIX}")
ADD_DEPENDENCIES(mariadbclient mariadb_connector_c GenError)
ADD_LIBRARY(libmariadb STATIC IMPORTED GLOBAL)
IF(WIN32)
SET_TARGET_PROPERTIES(libmariadb PROPERTIES IMPORTED_LOCATION
  "${CONNECTOR_C_INSTALLDIR}/lib/mariadb/libmariadb${CMAKE_STATIC_LIBRARY_SUFFIX}")
ELSE()
SET_TARGET_PROPERTIES(libmariadb PROPERTIES IMPORTED_LOCATION
  "${CONNECTOR_C_INSTALLDIR}/lib/mariadb/${CMAKE_STATIC_LIBRARY_PREFIX}mysql${CMAKE_STATIC_LIBRARY_SUFFIX}")
SET_TARGET_PROPERTIES(mariadbclient PROPERTIES IMPORTED_LINK_INTERFACE_LIBRARIES "${CC_LIBS}")
ENDIF()
ADD_DEPENDENCIES(libmariadb mariadb_connector_c)

SET(CONNECTOR_C_LIBS 
  ${CONNECTOR_C_INSTALLDIR}/lib/mariadb/${CMAKE_STATIC_LIBRARY_PREFIX}mariadbclient${CMAKE_STATIC_LIBRARY_SUFFIX}  ${CC_LIBS} PARENT_SCOPE)
 
  